apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'quadible'
    bintrayName = 'mvp'

    publishedGroupId = 'com.quadible'
    libraryName = 'mvp'
    artifact = 'mvp'

    libraryDescription = 'A simple implementation of mvp in android'

    siteUrl = 'https://github.com/St4B/mvp'
    gitUrl = 'https://github.com/St4B/mvp.git'

    libraryVersion = '1.1Lambda'

    developerId = 'St4B'
    developerName = 'Vaios Tsitsonis'
    developerEmail = 'v.tsitsonis@quadible.co.uk'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compileOnly 'com.android.support:appcompat-v7:27.1.1'
    compileOnly 'com.google.code.gson:gson:2.+'

    api project(':mvp-annotation')

    testImplementation 'junit:junit:4.12'
}

//Apply at the end!
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/" + artifact + "-release.aar")
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}